title: Net(1).exe Query Statistics
id: 60acbbd2-8514-4c97-9b92-5c2b9687a8e4
status: experimental
level: medium
description: |
  This query can be used to list the statistics of the entities that have been queried in the last x days. The x is determined by the *StartTime* parameter. Only the (local)group and user query types are included in this query. This query can be used to list the user/groups that are often queried or to list rare discovery activities.
author: Bert-JanP
logsource:
  product: microsoft_defender_endpoint
  service: kql
detection:
  _kql_query: |
    let StartTime = 30d;
    DeviceProcessEvents
    | where Timestamp > startofday(ago(StartTime))
    | where FileName in ("net.exe", "net1.exe")
    | extend NetActionType = case(ProcessCommandLine has "accounts", "ACCOUNTS",
    ProcessCommandLine has "group", "GROUP",
    ProcessCommandLine has "user", "USER",
    ProcessCommandLine has "localgroup", "LOCALGROUP",
    "Other")
    | where NetActionType != "Other"
    | where isnotempty(AccountUpn)
    | extend ExtractedParameters = split(ProcessCommandLine, " ")
    | mv-apply QueriedEntity = ExtractedParameters on (
    where not(QueriedEntity has_any ("net", "net1", "user", "group", @"/do", @"/domain", @"/dom"))
    | project QueriedEntity
    )
    | where isnotempty(QueriedEntity)
    | extend QueriedEntity = tolower(QueriedEntity)
    | summarize arg_max(Timestamp, *) by ReportId
    | summarize TotalQueries = count() by QueriedEntity, NetActionType
    | sort by TotalQueries
  condition: _kql_query
references:
  - https://github.com/Bert-JanP/Hunting-Queries-Detection-Rules
